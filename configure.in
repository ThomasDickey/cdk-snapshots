dnl $Id: configure.in,v 1.56 2012/03/22 09:28:59 tom Exp $
dnl Process this file with autoconf to produce a configure script for CDK.
dnl
AC_INIT(include/cdk.h)
AC_CONFIG_HEADER(include/cdk_config.h:include/config.hin)

CF_CHECK_CACHE
CF_VERSION_INFO(cdk)

dnl
dnl Checks for programs.
dnl
AC_PROG_CC
AC_PROG_CPP
AC_PROG_GCC_TRADITIONAL
AC_PROG_RANLIB 
AC_PROG_INSTALL
CF_PROG_LN_S

dnl needed for CF_WITH_LIBTOOL
AC_CHECK_TOOL(AR, ar, ar)

AC_PROG_MAKE_SET
CF_MAKEFLAGS
CF_MAKE_TAGS

CF_ANSI_CC_REQD
AC_C_CONST
CF_XOPEN_SOURCE

CF_DISABLE_ECHO
CF_PROG_EXT
CF_LIB_PREFIX

AC_ARG_WITH(man-section,
	[  --with-man-section=XXX  change manpage section (default 3)],
	[MANSECT=$with_man_section],
	[MANSECT=3])
case .$MANSECT in
.yes|.no)
	MANSECT=3
	;;
esac
MAN_TAG=`echo $MANSECT | sed -e 's/^\(.\).*/\1/'`
AC_SUBST(MAN_TAG)
AC_SUBST(MANSECT)

CF_WITH_CURSES_DIR
CF_WITH_WARNINGS
CF_PKG_CONFIG

CF_WITH_LIBTOOL
if test "$with_libtool" = "yes" ; then
	OBJEXT="lo"
fi

dnl
dnl Checks for libraries.
dnl

CF_DISABLE_LEAKS

dnl
dnl Checks for header files.
dnl
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(\
limits.h \
unistd.h \
pwd.h \
grp.h \
)

dnl
dnl Checks for typedefs, structures, and compiler characteristics.
dnl
AC_TYPE_MODE_T
AC_STRUCT_TM
CF_CURSES_CHTYPE
CF_GETOPT_HEADER

dnl
dnl Checks for library functions.
dnl
AC_CHECK_FUNCS(\
getcwd \
mktime \
strdup \
strerror \
getlogin \
sleep \
)

CF_FUNC_LSTAT
CF_LOCALE([AC_DEFINE(HAVE_SETLOCALE)])

dnl Check for curses libraries last, since XCurses has dependencies which
dnl break the checks for other functions.
use_library=curses
AC_MSG_CHECKING(for library to use)
AC_ARG_WITH(ncurses,
	[  --with-ncurses          compile/link with ncurses library],
	[use_library=ncurses],[
AC_ARG_WITH(ncursesw,
	[  --with-ncursesw         compile/line with wide-character ncurses],
	[use_library=ncursesw],[
AC_ARG_WITH(pdcurses,
	[  --with-pdcurses         compile/link with pdcurses X11 library],
	[use_library=pdcurses])])])
AC_MSG_RESULT($use_library)

CFG_ROOTNAME=cdk
HDR_ROOTNAME=cdk
LIB_ROOTNAME=cdk

case $use_library in #(vi
ncurses) #(vi
	CF_NCURSES_CONFIG
	;;
ncursesw)
	CF_UTF8_LIB
	CF_NCURSES_CONFIG(ncursesw)
	LIB_ROOTNAME=cdkw
	;;
pdcurses) #(vi
	CF_PDCURSES_X11
	LIB_ROOTNAME=cdkX
	;;
*)
	CF_CURSES_CPPFLAGS
	CF_NCURSES_VERSION
	CF_CURSES_LIBS
	;;
esac

AC_MSG_CHECKING(for XXX-config rootname)
AC_ARG_WITH(cfgname,
	[  --with-cfgname=XXX      override the XXX-config rootname],
	[CFG_ROOTNAME=$withval])
AC_MSG_RESULT($CFG_ROOTNAME)
AC_SUBST(CFG_ROOTNAME)

AC_MSG_CHECKING(if cdk.h should be in header subdirectory)
AC_ARG_WITH(hdrname,
	[  --enable-hdr-subdir     install cdk.h in the header subdirectory],
	[HDR_ROOTNAME=no])
AC_MSG_RESULT($HDR_SUBDIR)
AC_SUBST(HDR_SUBDIR)

if test "$HDR_SUBDIR" = yes
then
	HDR_SUBDIR="#"
else
	HDR_SUBDIR=
fi

AC_MSG_CHECKING(for header subdirectory)
AC_ARG_WITH(hdrname,
	[  --with-hdrname=XXX      override the header subdirectory],
	[HDR_ROOTNAME=$withval])
AC_MSG_RESULT($HDR_ROOTNAME)
AC_SUBST(HDR_ROOTNAME)

AC_MSG_CHECKING(for library name)
AC_ARG_WITH(libname,
	[  --with-libname=XXX      override the library name],
	[LIB_ROOTNAME=$withval])
AC_MSG_RESULT($LIB_ROOTNAME)
AC_SUBST(LIB_ROOTNAME)

AC_MSG_CHECKING(if improved const-typing should be used)
AC_ARG_ENABLE(const,
	[  --enable-const          use improved const-typing],
	[CDK_CONST=$enableval],
	[CDK_CONST=no])
AC_MSG_RESULT($CDK_CONST)
if test "$CDK_CONST" = yes
then
	AC_DEFINE(CDK_CONST,const)
else
	AC_DEFINE(CDK_CONST,/*nothing*/)
fi

# define these for casting convenience
AC_DEFINE(CDK_CSTRING,CDK_CONST char *)
AC_DEFINE(CDK_CSTRING2,CDK_CONST char * CDK_CONST *)

CF_CURSES_FUNCS(\
	Xinitscr\
	getbegx \
	getbegy \
	getmaxx \
	getmaxy \
	start_color \
	\
)

CF_WITH_NC_ALLOC_H
CF_CURSES_SETBEGYX

dnl
dnl Set pre-processor compile time variables.
dnl
AC_OUTPUT(
	cdk-config
	include/cdk_version.h:include/cdk_version.hin
	Makefile
	cli/Makefile
	examples/Makefile
	demos/Makefile,,,sort)
