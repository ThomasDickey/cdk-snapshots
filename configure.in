dnl
dnl Process this file with autoconf to produce a configure script.
dnl
AC_INIT(include/cdk.h)

dnl
dnl Checks for programs.
dnl
AC_PROG_CC
AC_PROG_RANLIB 
AC_PROG_INSTALL

CF_DISABLE_ECHO

dnl
dnl Checks for libraries. If we have start_color
dnl set the HAVE_COLOR define.
dnl
AC_CHECK_LIB(m,sin)

AC_ARG_WITH(warnings,
[  --with-warnings         turn on gcc warnings, for debugging ],[
	CF_GCC_WARNINGS
])

AC_ARG_WITH(ncurses,
[  --with-ncurses          compile/link with ncurses library],
[	
	CF_NCURSES_CPPFLAGS
	CF_NCURSES_LIBS
	],[
	CF_CURSES_LIBS
])
AC_CHECK_FUNC(start_color,[AC_DEFINE(HAVE_COLOR)])
test "$cf_cv_have_lib_ncurses" = yes && CFLAGS="$CFLAGS -DHAVE_LIBNCURSES"

dnl
dnl Checks for header files.
dnl
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h unistd.h)

dnl
dnl Checks for typedefs, structures, and compiler characteristics.
dnl
AC_TYPE_MODE_T
AC_STRUCT_TM
CF_CURSES_CHTYPE

dnl
dnl Checks for library functions.
dnl
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(getcwd mktime strdup strerror usleep)

dnl
dnl Set pre-processor compile time variables.
dnl
AC_CHECK_FUNC(XCursesExit, AC_DEFINE(HAVE_XCURSES))

AC_OUTPUT(Makefile examples/Makefile demos/Makefile)
